<html>
<head>
	<title>frame differencing</title>
	<style>
		body{
			margin: 0;
		}
	</style>
</head>
<body>
  <script id="vs" type="x-shader/x-vertex">

    varying vec2 vUv;
    uniform float time;
    void main() {
        vUv = uv;
        gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
    }
</script>
<script id="baseFs" type="x-fragment/x-shader">
uniform sampler2D texture; 
uniform sampler2D texture2; 
uniform float time; // seconds
varying vec2 vUv;
uniform vec2 resolution;

void main() {
    gl_FragColor = texture2D(texture2, vUv);
}
</script>

<script id="blurFrag" type="x-shader/x-fragment">
uniform sampler2D texture;
uniform vec2 resolution;


varying vec2 vUv;

void main() {
  float step_w = 1.75/resolution.x;
  float step_h = 1.75/resolution.y;
  vec2 tc = vUv;
  vec4 input0 = texture2D(texture,tc);
   
  vec2 x1 = vec2(step_w, 0.0);
  vec2 y1 = vec2(0.0, step_h);
    
  input0 += texture2D(texture, tc+x1); // right
  input0 += texture2D(texture, tc-x1); // left
  input0 += texture2D(texture, tc+y1); // top
  input0 += texture2D(texture, tc-y1); // bottom

  input0 *=0.2;

  gl_FragColor = input0;
}
</script>



<script id="contrastFrag" type="x-shader/x-fragment">
uniform sampler2D texture;
varying vec2 vUv;

//float contrast = 2.0;

void main() {
    vec4 color = texture2D(texture, vec2((vUv.x*-1.0)+1.0, vUv.y)); //horizontal flip
    //const vec3 LumCoeff = vec3(0.2125, 0.7154, 0.0721);
    
   //vec3 AvgLumin = vec3(0.5, 0.5, 0.5);
    
    //vec3 intensity = vec3(dot(color.rgb, LumCoeff));
    
    //vec3 satColor = mix(intensity, color.rgb, 1.);
    //vec3 conColor = mix(AvgLumin, satColor, contrast);

    gl_FragColor = vec4(vec3(color.r),1.0);
}
</script>


<script id="flowFrag" type="x-shader/x-fragment">

varying vec2 vUv;
uniform sampler2D texture;
uniform sampler2D texture2;
vec2 scale = vec2(200.0,200.0);
uniform vec2 resolution;
float lambda = 50.0;
const vec4 lumcoeff = vec4(0.299,0.587,0.114,0.);

void main()
{   
  vec4 a = texture2D(texture, vUv);
  vec4 b = texture2D(texture2, vUv);
  vec2 offset = vec2(1.0/resolution.x, 1.0/resolution.y);
  vec2 x1 = vec2(offset.x,0.);
  vec2 y1 = vec2(0.,offset.y);

  vec4 curdif = b-a;
  
  vec4 gradx = texture2D(texture2, vUv+x1)-texture2D(texture2, vUv-x1);
  gradx += texture2D(texture, vUv+x1)-texture2D(texture, vUv-x1);
  
  vec4 grady = texture2D(texture2, vUv+y1)-texture2D(texture2, vUv-y1);
  grady += texture2D(texture, vUv+y1)-texture2D(texture, vUv-y1);
  
  vec4 gradmag = sqrt((gradx*gradx)+(grady*grady)+vec4(lambda));

  vec4 vx = curdif*(gradx/gradmag);
  float vxd = vx.r;
  vec2 xout = vec2(max(vxd,0.),abs(min(vxd,0.)))*scale.x;

  vec4 vy = curdif*(grady/gradmag);
  float vyd = vy.r;
  vec2 yout = vec2(max(vyd,0.),abs(min(vyd,0.)))*scale.y;
  
  gl_FragColor = vec4(xout.xy,yout.xy);
    
}

</script>

<script id="reposFrag" type="x-shader/x-fragment">
varying vec2 vUv;

uniform vec2 amt;
uniform sampler2D texture;
uniform sampler2D texture2;


void main(){

    vec2 tc = vUv;
    vec4 look = texture2D(texture2,tc);//sample repos texture
    vec2 offs = vec2(look.y-look.x,look.w-look.z)*vec2(0.05,0.05);
    vec2 coord = offs+tc;  //relative coordinates
    
    vec4 repos = texture2D(texture, coord);
    
    gl_FragColor = repos;    
}
</script>

<script id="blackFrag" type="x-shader/x-fragment">

void main() {
  gl_FragColor = vec4(0.0,0.0,0.0,0.0);
}
</script>
<script src="../three.min.js"></script>
<script src="../BinaryLoader.js"></script>
<script src="../Detector.js"></script>
<script src="../OrbitControls.js"></script>
<script src="../dat.gui.min.js"></script>
<script src="gush.js"></script>
</body>
</html>